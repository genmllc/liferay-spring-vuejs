buildscript {
	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}

	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.css.builder", version: "3.0.3"
	}
}

apply plugin: "com.liferay.css.builder"
apply plugin: "war"

configurations {
	vuejsResources
}

dependencies {
	// Probably not the best solution but it's working so...
	runtimeOnly ( project(':vuejs-app') ) //force gradle to build the front-end jar
	vuejsResources ( project(':vuejs-app') ) //force gradle to unpack the front-end jar

	compile group: "com.liferay.portletmvc4spring", name: "com.liferay.portletmvc4spring.framework", version: "5.2.1"
	compile group: "com.liferay.portletmvc4spring", name: "com.liferay.portletmvc4spring.security", version: "5.2.1"
	compile(group: "org.hibernate.validator", name: "hibernate-validator", version: "6.1.5.Final") {
		exclude group: "javax.validation", module: "validation-api"
	}
	
	compile group: "org.springframework.security", name: "spring-security-config", version: '5.2.3.RELEASE'
	compile group: "org.springframework.security", name: "spring-security-core", version: '5.2.3.RELEASE'
	compile group: "org.springframework.security", name: "spring-security-web", version: '5.2.3.RELEASE'
	compile group: "org.springframework", name: "spring-aop", version: '5.2.8.RELEASE'
	compile group: "org.springframework", name: "spring-beans", version: '5.2.8.RELEASE'
	compile group: "org.springframework", name: "spring-context", version: '5.2.8.RELEASE'
	compile group: "org.springframework", name: "spring-core", version: '5.2.8.RELEASE'
	compile group: "org.springframework", name: "spring-expression", version: '5.2.8.RELEASE'
	compile group: "org.springframework", name: "spring-jcl", version: '5.2.8.RELEASE'
	compile group: "org.springframework", name: "spring-web", version: '5.2.8.RELEASE'
	compile group: "org.springframework", name: "spring-webmvc"	, version: '5.2.8.RELEASE'
	compile(group: "org.thymeleaf", name: "thymeleaf", version: '3.0.11.RELEASE') {
		exclude group: "ognl", module: "ognl"
		exclude group: "org.slf4j", module: "slf4j-api"
	}

	compile(group: "org.thymeleaf", name: "thymeleaf-spring5", version: '3.0.11.RELEASE') {
		exclude group: "org.slf4j", module: "slf4j-api"
	}
	
	
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: '8.1.0'
	compileOnly group: "javax.portlet", name: "portlet-api", version: '3.0.1'
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: '4.0.1'
	compileOnly group: "javax.validation", name: "validation-api", version: "2.0.1.Final"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: '1.4.0'

	// Logging
	compile group: "org.apache.logging.log4j", name: "log4j-slf4j-impl", version: "2.13.3"
	
	// JSON
	compile group: "com.fasterxml.jackson.datatype", name: "jackson-datatype-jsr310", version: "2.10.5"
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.10.5'
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.10.5'	
}

repositories {
	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

war {
	dependsOn buildCSS
	exclude "**/*.scss"

	filesMatching("**/.sass-cache/") {
		it.path = it.path.replace(".sass-cache/", "")
	}

	// Unpack the front-end jar
	with copySpec {
		from (zipTree(configurations.vuejsResources.singleFile)) {
			into 'WEB-INF/views/'
		}
	}

	archiveBaseName = 'liferay-spring-vuejs'
	includeEmptyDirs = false
}

// Deploys the war on Liferay server
task deploy(type: Copy) {
	group 'build'
	dependsOn war
	from war.archivePath
	into "../../liferay/bundles/deploy"
}